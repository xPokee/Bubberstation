// Connects a client to voice chat via an external browser
/datum/controller/subsystem/voicechat/proc/join_vc(client/C, show_link_only=FALSE)
	var/node_port = CONFIG_GET(number/port_voicechat) // I see no good reasons why admins should be able to modify the port so we check config every time
	if(!C)
		return
	// Disconnect existing session if present
	var/existing_userCode = client_userCode_map[C]
	if(existing_userCode)
		disconnect(existing_userCode, from_byond = TRUE)

	// Generate unique session and user codes
	var/sessionId = md5("[world.time][rand()][world.realtime][rand(0,9999)][C.address][C.computer_id]")
	var/userCode = generate_userCode(C)
	if(!userCode)
		return

	// Open external browser with voice chat link
	var/address = src.domain || world.internet_address
	var/web_link = "https://[address]:[node_port]?sessionId=[sessionId]"
	if(!show_link_only)
		C << link(web_link)
	else
		C << browse({"
		<html>
			<body>
				<h3>[web_link]</h3>
				<p>copy and paste the link into your web browser of choice, or scan the qr code.</p>
				<img src="https://api.qrserver.com/v1/create-qr-code/?data=${encodeURIComponent([web_link])}&size=150x150">
			</body>
		</html>"}, "window=voicechat_help")


	send_json(alist(
		cmd = "register",
		userCode = userCode,
		sessionId = sessionId
	))

	// Link client to userCode
	userCode_client_map[userCode] = C
	client_userCode_map[C] = userCode
	// Confirmation handled in confirm_usekrCode


// Confirms userCode when browser and mic access are granted
/datum/controller/subsystem/voicechat/proc/confirm_userCode(userCode)
	if(!userCode || (userCode in vc_clients))
		return
	var/client/C = userCode_client_map[userCode]
	var/mob/M = C.mob
	if(!C || !M)
		disconnect(userCode)
		return
	mob_client_map[M] = C

	vc_clients += userCode
	register_mob_signals(M)
	check_mob_conditions(M)
	RegisterSignal(C, COMSIG_QDELETING, PROC_REF(on_client_leaving_game))


/datum/controller/subsystem/voicechat/proc/register_mob_signals(mob/M)
	SIGNAL_HANDLER
	RegisterSignal(M, COMSIG_MOB_LOGOUT, PROC_REF(mob_changed))


	if(isliving(M))
		RegisterSignals(M, list(\
			SIGNAL_ADDTRAIT(TRAIT_KNOCKEDOUT),
			SIGNAL_ADDTRAIT(TRAIT_DEAF),
			SIGNAL_ADDTRAIT(TRAIT_MUTE),
			SIGNAL_ADDTRAIT(TRAIT_MIMING),
			), PROC_REF(clear_from_room))
		RegisterSignals(M, list(\
			SIGNAL_REMOVETRAIT(TRAIT_KNOCKEDOUT),
			SIGNAL_REMOVETRAIT(TRAIT_DEAF),
			SIGNAL_REMOVETRAIT(TRAIT_MUTE),
			SIGNAL_REMOVETRAIT(TRAIT_MIMING)
			), PROC_REF(add_to_room))
		RegisterSignal(M, COMSIG_LIVING_DEATH, PROC_REF(on_mob_death))
		RegisterSignal(M, COMSIG_LIVING_REVIVE, PROC_REF(on_mob_revive))
/datum/controller/subsystem/voicechat/proc/mob_changed(mob/M)
	var/client/C = mob_client_map[M]
	var/mob/new_mob = C.mob
	if(!C || !new_mob)
		return

	mob_client_map.Remove(M)
	mob_client_map[new_mob] = C
	unregister_mob_signals(M)

	register_mob_signals(new_mob)
	check_mob_conditions(new_mob)

/datum/controller/subsystem/voicechat/proc/unregister_mob_signals(mob/M)
	UnregisterSignal(M, COMSIG_MOB_LOGOUT)
	if(isliving(M))
		UnregisterSignal(M, list(\
			SIGNAL_ADDTRAIT(TRAIT_KNOCKEDOUT),
			SIGNAL_ADDTRAIT(TRAIT_DEAF),
			SIGNAL_ADDTRAIT(TRAIT_MUTE),
			SIGNAL_ADDTRAIT(TRAIT_MIMING),
			SIGNAL_REMOVETRAIT(TRAIT_KNOCKEDOUT),
			SIGNAL_REMOVETRAIT(TRAIT_DEAF),
			SIGNAL_REMOVETRAIT(TRAIT_MUTE),
			SIGNAL_REMOVETRAIT(TRAIT_MIMING),
			COMSIG_LIVING_DEATH,
			COMSIG_LIVING_REVIVE,
		))


/datum/controller/subsystem/voicechat/proc/clear_from_room(mob/M)
	SIGNAL_HANDLER
	if(!M)
		CRASH("signal called without user {usr: [usr || "null"]}")
	var/client/C = M.client
	var/userCode = client_userCode_map[C]
	if(!C || !userCode)
		return
	clear_userCode(userCode)

/datum/controller/subsystem/voicechat/proc/add_to_room(mob/M)
	SIGNAL_HANDLER
	if(!M)
		CRASH("signal called without user {usr: [usr || "null"]}")
	var/client/C = M.client
	var/userCode = client_userCode_map[C]
	if(!C || !userCode)
		return
	move_userCode_to_room(userCode, "living")

/datum/controller/subsystem/voicechat/proc/on_mob_death(mob/M)
	SIGNAL_HANDLER
	check_mob_conditions(M)

/datum/controller/subsystem/voicechat/proc/on_mob_revive(mob/M)
	SIGNAL_HANDLER
	check_mob_conditions(M)


/datum/controller/subsystem/voicechat/proc/check_mob_conditions(mob/M)
	if(!M)
		return

	var/client/C = M.client
	var/userCode = client_userCode_map[C]

	if(!C || !userCode)
		return



	var/room

	// everyone goes to no prox to yell at each other at round end.
	if(isnewplayer(M) || SSticker.current_state == GAME_STATE_FINISHED)
		room = "lobby"

	else if(isdead(M) || M.stat == DEAD)
		room = "ghost"

	else if(isliving(M))
		if(HAS_TRAIT(M, TRAIT_KNOCKEDOUT) || HAS_TRAIT(M, TRAIT_DEAF)|| HAS_TRAIT(M, TRAIT_MUTE) || HAS_TRAIT(M, TRAIT_MIMING))
			clear_from_room(M)
		else
			room = "living"

	if(room && userCode_room_map[userCode] != room)
		move_userCode_to_room(userCode, room)
		//for lobby chat as ticker isnt intialized.
		if(SSticker.current_state < GAME_STATE_PLAYING)
			send_locations()

/datum/controller/subsystem/voicechat/proc/on_client_leaving_game(client/C)
	var/userCode = client_userCode_map[C]
	if(!userCode)
		message_admins("disconnecting voicechat user didnt work. {client : [C || "null"], userCode: [userCode || "null"]}")
		return
	disconnect(userCode, from_byond = TRUE)

// Disconnects a user from voice chat
/datum/controller/subsystem/voicechat/proc/disconnect(userCode, from_byond = FALSE)
	if(!userCode)
		return
	toggle_active(userCode, FALSE)
	clear_userCode(userCode)

	var/client/C = userCode_client_map[userCode]
	if(C)
		userCode_client_map.Remove(userCode)
		client_userCode_map.Remove(C)
		userCode_room_map.Remove(userCode)
		vc_clients -= userCode

	var/mob/M = C.mob

	if(userCodes_speaking_icon[userCode])
		if(C && M)
			M.cut_overlay(userCodes_speaking_icon[userCode])
			unregister_mob_signals(M)

	if(from_byond)
		send_json(alist(cmd= "disconnect", userCode= userCode))
	//for lobby chat

	if(SSticker.current_state < GAME_STATE_PLAYING)
		send_locations()



// Toggles the speaker overlay for a user
/datum/controller/subsystem/voicechat/proc/toggle_active(userCode, is_active)
	if(!userCode || isnull(is_active))
		return
	var/client/C = userCode_client_map[userCode]

	if(!C || !C.mob)
		return
	var/mob/M = C.mob
	if(!userCodes_speaking_icon[userCode])
		var/image/speaker = image('modular_zubbers/icons/mob/effects/talk.dmi', icon_state = "voice")
		speaker.alpha = 200
		userCodes_speaking_icon[userCode] = speaker

	var/image/speaker = userCodes_speaking_icon[userCode]
	var/mob/old_mob = userCode_mob_map[userCode]
	if(M != old_mob)
		if(old_mob)
			old_mob.overlays -= speaker
		userCode_mob_map[userCode] = M
	var/room = userCode_room_map[userCode]
	if(is_active && room && !M.stat)
		userCodes_active |= userCode
		M.add_overlay(speaker)
	else
		userCodes_active -= userCode
		M.cut_overlay(speaker)


// Mutes or deafens a user's microphone
/datum/controller/subsystem/voicechat/proc/mute_mic(client/C, deafen = FALSE)
	if(!C)
		return
	var/userCode = client_userCode_map[C]
	if(!userCode)
		return
	send_json(list(
		cmd = deafen ? "deafen" : "mute_mic",
		userCode = userCode
	))
